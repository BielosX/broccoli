plugins {
    id 'java-library'
    id 'io.freefair.lombok' version '8.4'
    id 'maven-publish'
    id 'broccoli.java-feign-generator' version '1.0.0'
}

java {
    sourceCompatibility 17
}

repositories {
    maven {
        url repositoryUrl
        credentials {
            username repositoryUser
            password repositoryPassword
        }
    }
}

ext {
    jackson_version = "2.16.1"
    jackson_databind_version = "2.16.1"
    jackson_databind_nullable_version = "0.2.6"
    jakarta_annotation_version = "3.0.0-M1"
    feign_version = "13.1"
    feign_form_version = "3.8.0"
}

dependencies {
    api "io.github.openfeign:feign-core:$feign_version"
    implementation "io.github.openfeign:feign-jackson:$feign_version"
    implementation "io.github.openfeign:feign-slf4j:$feign_version"
    implementation "io.github.openfeign:feign-okhttp:$feign_version"
    implementation "io.github.openfeign.form:feign-form:$feign_form_version"
    implementation "com.fasterxml.jackson.core:jackson-core:$jackson_version"
    implementation "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"
    implementation "jakarta.annotation:jakarta.annotation-api:$jakarta_annotation_version"
    implementation "org.openapitools:jackson-databind-nullable:$jackson_databind_nullable_version"
}

javaFeignGenerator {
    apiPackage = 'org.github.client.api'
    modelPackage = 'org.github.client.model'
    inputSpec = "${projectDir}/openapi/api.github.com.json"
    outputDir = "${projectDir}/build/generated/openapi"
}

sourceSets {
    main {
        java {
            srcDir "${projectDir}/build/generated/openapi/src/main"
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = property('groupId')
            artifactId = property('artifactId')
            version = property('version')

            from components.java
        }
    }
    repositories {
        maven {
            url repositoryUrl
            credentials {
                username repositoryUser
                password repositoryPassword
            }
        }
    }
}

project.tasks.compileJava.dependsOn project.tasks.generateJavaFeignClient
